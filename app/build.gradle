apply plugin: 'com.android.application'

android
{
    compileSdkVersion 23
    buildToolsVersion "21.1.2"

    sourceSets
    {
        main
        {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['assets']
        }
    }

    defaultConfig
    {
        applicationId "com.forrestguice.suntimeswidget"
        minSdkVersion 10
        targetSdkVersion 23
        versionCode 7
        versionName "0.2.2"

        buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + getDateAsMillis() + "L)"
        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
    }

    buildTypes
    {
        release
        {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions
    {
        disable 'MissingTranslation'
    }
}

dependencies
{
    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'com.github.forrestguice:sunrisesunsetlib-java:master-SNAPSHOT'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

def getDateAsMillis()
{
    def Calendar cal = Calendar.getInstance();
    return cal.getTimeInMillis().toString()
}

def getGitHash()
{
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}